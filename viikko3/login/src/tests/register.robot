*** Settings ***
Resource  resource.robot
Suite Setup     Open And Configure Browser
Suite Teardown  Close Browser
Test Setup      Reset Application Create User And Go To Register Page

*** Test Cases ***

Register With Valid Username And Password
    Set Username  jakke
    Set Password  jakke123
    Set Password Confirmation  jakke123
    Click Button  Register
    Register Should Succeed

Register With Too Short Username And Valid Password
    Set Username  mi
    Set Password  mia12345
    Set Password Confirmation  mia12345
    Click Button  Register
    Register Should Fail With Message  Username should have at least 3 characters

Register With Valid Username And Too Short Password
    Set Username  laura
    Set Password  laura1
    Set Password Confirmation  laura1
    Click Button  Register
    Register Should Fail With Message  Password should have at least 8 characters


Register With Valid Username And Invalid Password
    Set Username  tommi
    Set Password  tommisala
    Set Password Confirmation  tommisala
    Click Button  Register
    Register Should Fail With Message  Password should contain at least one number

Register With Nonmatching Password And Password Confirmation
    Set Username  antti
    Set Password  antti123
    Set Password Confirmation  antti312
    Click Button  Register
    Register Should Fail With Message  Password and password confirmation do not match

Register With Username That Is Already In Use
    Set Username  kalle
    Set Password  kalle123
    Set Password Confirmation  kalle123
    Click Button  Register
    Register Should Fail With Message  User with username kalle already exists

*** Keywords ***
Register Should Succeed
    Welcome Page Should Be Open

Register Should Fail With Message
    [Arguments]  ${message}
    Register Page Should Be Open
    Page Should Contain  ${message}

Set Username
    [Arguments]  ${username}
    Input Text  username  ${username}

Set Password
    [Arguments]  ${password}
    Input Password  password  ${password}

Set Password Confirmation
    [Arguments]  ${password_confirmation}
    Input Password  password_confirmation  ${password_confirmation}

*** Keywords ***
Reset Application Create User And Go To Register Page
    Reset Application
    Create User  kalle  kalle123
    Go To Register Page
